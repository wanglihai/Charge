apply plugin: 'com.android.application'
/**
 * 在项目根路径下创建一个文件key.properties
 */
def keystorePropertiesFile = rootProject.file('key.properties')
//创建一个properties对象
def keystoreProperties = new Properties()
//加载key.properties里面的内容
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {


    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.znxk.charge"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"


    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile']) //签名文件
            storePassword keystoreProperties['storePassword']  //签名文件密码
            keyAlias keystoreProperties['keyAlias']       //签名文件别名
            keyPassword keystoreProperties['keyPassword']     //别名密码
        }

        debug {
            storeFile file(keystoreProperties['storeFile']) //签名文件
            storePassword keystoreProperties['storePassword']  //签名文件密码
            keyAlias keystoreProperties['keyAlias']       //签名文件别名
            keyPassword keystoreProperties['keyPassword']     //别名密码
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true  // Zipalign优化
            shrinkResources true // 移除无用的resource文件,必须和minifyEnabled 一起使用
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable true
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    //设置包名
    applicationVariants.all { variant ->
        def buildType = variant.buildType.name
        //android studio3.0之前命名
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //应用名+版本号+渠道名+构建类型+时间戳${variant.flavorName}
                def fileName = "znxk_V${defaultConfig.versionName}_${variant.flavorName}_${releaseTime()}_${buildType}.apk"
                //APK输出到默认目录
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
        //android studio3.0之后命名
//        variant.outputs.all {
//            outputFileName = "green_v${defaultConfig.versionName}_${variant.flavorName}_${releaseTime()}_${buildType}.apk"
//        }
    }

    productFlavors {//多渠道打包
        //命名规则和java一样，不能数字打头，不能纯数字
        zhushou360 {}
        xiaomi {}
    }

    productFlavors.all { flavor ->
        //为manifest中meta-data的占位符进行赋值
        flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

//发布时间
def releaseTime() {
    //设置发布时间的时候不要设置小时，这样的话每个小时生成的包都不一样，在安装时studio会报找不见apk的异常
//    return new Date().format('MMddHH', TimeZone.getTimeZone('GMT+8'))
    return new Date().format('MMdd', TimeZone.getTimeZone('GMT+8'))
}

//使用gradle打包的时候出现好多警告,如何忽略大部分的警告呢,使用如下配置即可
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // Try to turn them all off automatically
            options.compilerArgs << '-Xlint:none'
            options.compilerArgs << '-nowarn' // same as '-Xlint:none'

            // Turn them off manually
            options.compilerArgs << '-Xlint:-auxiliaryclass'
            options.compilerArgs << '-Xlint:-cast'
            options.compilerArgs << '-Xlint:-classfile'
            options.compilerArgs << '-Xlint:-deprecation'
            options.compilerArgs << '-Xlint:-dep-ann'
            options.compilerArgs << '-Xlint:-divzero'
            options.compilerArgs << '-Xlint:-empty'
            options.compilerArgs << '-Xlint:-fallthrough'
            options.compilerArgs << '-Xlint:-finally'
            options.compilerArgs << '-Xlint:-options'
            options.compilerArgs << '-Xlint:-overloads'
            options.compilerArgs << '-Xlint:-overrides'
            options.compilerArgs << '-Xlint:-path'
            options.compilerArgs << '-Xlint:-processing'
            options.compilerArgs << '-Xlint:-rawtypes'
            options.compilerArgs << '-Xlint:-serial'
            options.compilerArgs << '-Xlint:-static'
            options.compilerArgs << '-Xlint:-try'
            options.compilerArgs << '-Xlint:-unchecked'
            options.compilerArgs << '-Xlint:-varargs'
        }
    }
}



dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:support-v4:25.0.0'
    /*glide图片加载*/
    compile 'com.github.bumptech.glide:glide:4.6.1'
    compile 'com.github.bumptech.glide:okhttp3-integration:4.6.1'
    /*日志打印*/
    compile 'com.orhanobut:logger:2.2.0'
    /*进程间通信*/
    compile 'org.greenrobot:eventbus:3.1.1'

    compile 'com.squareup.retrofit2:retrofit:2.4.0'
    compile 'com.squareup.retrofit2:converter-gson:2.4.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.4.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    compile 'io.reactivex.rxjava2:rxjava:2.1.12'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.2'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    compile 'com.github.bumptech.glide:glide:4.6.1'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'
}
